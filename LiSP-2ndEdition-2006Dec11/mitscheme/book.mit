;;; $Id: book.elk,v 1.1 1996/01/29 19:44:09 queinnec Exp $

;;;(((((((((((((((((((((((((((((((( L i S P ))))))))))))))))))))))))))))))))
;;; This file is part of the files that accompany the book:
;;;     LISP Implantation Semantique Programmation (InterEditions, France)
;;; By Christian Queinnec <Christian.Queinnec@INRIA.fr>
;;; Newest version may be retrieved from:
;;;   (IP 128.93.2.54) ftp.inria.fr:INRIA/Projects/icsla/Books/LiSP*.tar.gz
;;; Check the README file before using this file.
;;;(((((((((((((((((((((((((((((((( L i S P ))))))))))))))))))))))))))))))))

;;; This file is used to customize Mit-Scheme in order to run the source
;;; files of the book. It preloads Meroonet, and the test-driver. It
;;; may serve as a basis for other ports to other Scheme
;;; interpreters. Look for the IMPORTANT notes below.

;;; This variable is used in chap8k.scm to determine the underlying
;;; Scheme interpreter.

(define book-interpreter-support 'mit)

;;; **IMPORTANT 1(but easy)**
;;; Missing functions:

(define gensym
  (let ((counter 99))
    (lambda args
      (set! counter (+ counter 1))
      (string->symbol
       (string-append
        (if (pair? args) (car args) "G")
        (number->string counter) ) ) ) ) )

(define (atom? x) (not (pair? x)))

;;; I also like the iota function. (iota 0 4) -> (0 1 2 3)

(define (iota start end)
  (if (< start end)
      (cons start (iota (+ 1 start) end))
      '() ) )

(define (every? p . args)
  (let andmap ((args args) (value #t))
    (if (let any-at-end? ((ls args))
          (and (pair? ls)
               (or (not (pair? (car ls)))
                   (any-at-end? (cdr ls)))))
        value
        (let ((value (apply p (map car args))))
          (and value (andmap (map cdr args) value))))))

(define (any? p . args)
  (let ormap ((args args) (value #f))
    (if (let any-at-end? ((ls args))
          (and (pair? ls)
               (or (not (pair? (car ls)))
                   (any-at-end? (cdr ls)))))
        value
        (let ((value (apply p (map car args))))
          (or value (ormap (map cdr args) value))))))

(define (symbol-append . args)
  (string->symbol
   (apply string-append
          (map (lambda (s)
                 (cond ((string? s) s)
                       ((symbol? s) (symbol->string s))
                       ((number? s) (number->string s))
                       (else (error 'symbol-append args)) ) )
               args ) ) ) )

;;; Name the Un*x ports and flush them.
;;; Seems that there is no stderr?

(define stdout-port (current-output-port))
(define stderr-port (current-output-port))

(define flush-buffer flush-output)

;;; Quick and dirty: sometimes very big objects are printed, limit
;;; them to something affordable.

(define *bounded-length* 4)
(define *bounded-depth* 3)

(define (bounded-display o stream)
  (define (print-list o* len dep)
    (cond ((null? o*) #t)
          ((atom? o*) (display " . " stream)
                      (display o* stream) )
          ((pair? o*) 
           (print (car o*) len (+ dep 1))
           (if (pair? (cdr o*)) (display " " stream))
           (print-list (cdr o*) (+ len 1) dep) ) ) )
  (define (print o len dep)
    (cond ((object? o) 
           (display "#<" stream)
           (display (Class-name (object->class o)) stream)
           (display ">" stream) )
          ((atom? o) (display o stream))
           (else (if (or (> len *bounded-length*)
                         (> dep *bounded-depth*) )
                     (display "&&&" stream)
                     (begin
                       (display "(" stream)
                       (print-list o len dep)
                       (display ")" stream) ) ) ) ) )
  (print o 0 0) )
;;; Test: (bounded-display (call-with-input-file "si/reflisp.scm" read) stdout-port)

;;; Sometimes property lists are seldom used.
;;; I did not find them quickly so I use a (slow) replacement.

(define putprop 'wait)
(define getprop 'wait)

(let ((properties '()))
  (set! putprop
        (lambda (symbol key value)
          (let ((plist (assq symbol properties)))
            (if (pair? plist)
                (let ((couple (assq key (cdr plist))))
                  (if (pair? couple)
                      (set-cdr! couple value)
                      (set-cdr! plist (cons (cons key value)
                                            (cdr plist) )) ) )
                (let ((plist (list symbol (cons key value))))
                  (set! properties (cons plist properties)) ) ) )
          value ) )
  (set! getprop
        (lambda (symbol key)
          (let ((plist (assq symbol properties)))
            (if (pair? plist)
                (let ((couple (assq key (cdr plist))))
                  (if (pair? couple)
                      (cdr couple)
                      #f ) )
                #f ) ) ) ) )

;;; pp is present (and autoloaded)

;;; Loads syntax-case V2.0. 
;;; Attention Mit-Scheme confuses #f and () as Scheme->C.

(syntax-table-define system-global-syntax-table 
    's2c-if
  (macro (condition . rest)
    `(if (let ((condition ,condition))
           (or condition (null? condition)) )
         . ,rest ) ) )

(load "mitscheme/others/compat.ss")
(load "mitscheme/others/hooks.mit")
(load "mitscheme/others/output.ss")
(load "mitscheme/others/init.ss")
(load "mitscheme/others/expand.sc")
(load "mitscheme/others/macro-defs.sc")

;;; This function loads a file expanded with syntax-expand.

(define *syntax-case-load-verbose?* #f)

(define (syntax-case-load file)
  (call-with-input-file file
    (lambda (in)
      (if *syntax-case-load-verbose?* 
          (begin (newline)
                 (display ";;; Loading ")
                 (display file)
                 (newline) ) )
      (let loop ((e (read in)))
        (if (eof-object? e) 
            file
            (let ((r (eval (expand-syntax e) top-level-environment)))
              (if *syntax-case-load-verbose?*
                  (begin (display ";= ")
                         (display r)
                         (newline) ) )
              (loop (read in)) ) ) ) ) ) )

;;; Defines specific locations for error handlers for meroonet.scm and
;;; tester.scm. They will be filled later.

(define meroonet-error 'wait)

(define tester-error 'wait)

;;; Load the test-driver (fortunately it does not need macros, so use
;;; load instead of syntax-case-load).

(load "src/tester.scm")

;;; Load Meroonet. Meroonet defines three macros with
;;; define-meroonet-macro. These macros are not needed by the rest of
;;; this file, so it is sufficient to register them with syntax-case.
;;; But, instead of syntax-case-loading the file, I only load it with 
;;; a definition of define-meroonet-macro that defines it for Dybvig.

(syntax-table-define system-global-syntax-table 
    'define-meroonet-macro
  (macro (ccall . bbody)
    (define (meroonet-make-expander call body x g)
      `(begin
         ;; define the Dybvig expander as a regular function:
         (define ,g
           (lambda (,x)
             ;; x receives a syntax-object and not a form
             (define (strip x) (vector-ref x 1))
             ;; Don't check arity, apply directly
             (apply (lambda ,(cdr call) . ,body)
                    (cdr (strip ,x)) ) ) )
         ;; register the Dybvig macro at load-time:
         (expand-syntax
          '(define-syntax ,(car call) ,g) ) ) )
    (meroonet-make-expander ccall bbody (gensym) (gensym)) ) )

(load "meroonet/meroonet.scm")

;;; **IMPORTANT 2**
;;; The test-driver should try to catch errors of the underlying Scheme 
;;; system. This is non-portable and difficult in many implementations. If
;;; do not succeed writing it, you can still run the programs of the book
;;; but you will not be able to run all the test-suites since some tests 
;;; (for instance in meroonet/oo-tests.scm) require errors to be caught
;;; when signalled by list-tail with a non-numeric second argument.

(set! error-handler
      (lambda error-msg
        (error-print error-msg) ) )

;;; Since the define-abbreviation is also necessary for the book when
;;; non high level macros are defined, register define-abbreviation
;;; for syntax-case. 

(expand-syntax
 '(define-syntax define-abbreviation
    (syntax-rules ()
      ((define-abbreviation call . body)
       (define-meroonet-macro call . body) ) ) ) )

;;; This function will test a suite of tests. It also allows to test the
;;; port of syntax-case.

(define (test file)
  (suite-test
   file "?? " "== " #t
   (lambda (read check err)
     (define (the-error . args)
       (apply err args) )
     (set! error-handler the-error)
     (set! meroonet-error the-error)
     (set! tester-error    the-error)
     (lambda ()
       (let ((e (read)))
         (check (eval (expand-syntax e) user-initial-environment)) ) ) )
   equal? ) )
;;; Test: 
;;;	(test "meroonet/oo-tests.scm")
;;;	(test "bigloo/others/syntax.tst")

(define (get-internal-run-time) 
  (real-time-clock) ) ; needed by (test "bigloo/others/syntax.tst")

;;; This variable is needed by meroonet/oo-tests.scm test suite.

(define the-Point 'useful4tests)

;;; Generally, when an error is detected in one of my programs, a
;;; <something>-error function is called which calls itself wrong. The
;;; wrong function allows the test-driver to be aware that something
;;; went wrong.

(define wrong 'wait)
(define static-wrong 'wait)

;;; The `show' and `clone' generic functions are predefined in Meroon
;;; not in Meroonet.  The problem is to define a generic function with
;;; Meroonet macros while these macros are only compiled and not yet
;;; present.

;;; The clone function that performs a shallow copy of a Meroonet object.

(eval (expand-syntax 
       '(begin
          (define-generic (show (o) . stream)
            (let ((stream (if (pair? stream) (car stream)
                              (current-output-port) )))
              (bounded-display o stream) ) )
          (define-generic (clone (o))
            (list->vector (vector->list o)) ) ) ))

;;; Define a new toplevel with syntax-case as macroexpander.
;;; A small toplevel loop that uses the syntax-case package of Hieb
;;; and Dybvig (define-syntax is used throughout the book). Since many
;;; of the tests of the book also use the load function, this one
;;; should be redefined to expand with syntax-case first.

(define (start)
  (display "[C. Queinnec's book] Mit-Scheme+Meroonet+syntax-case...")
  (newline)
  (set! *syntax-case-load-verbose?* #t)
  (set! load syntax-case-load)
  (interpreter
   "? " "= " #t
   (lambda (read print err)
     (set! error-handler  err)
     (set! tester-error   err)
     (set! meroonet-error err)
     (lambda ()
       (let ((e (read)))
         (print (eval (expand-syntax e) user-initial-environment)) ) ) ) )
  (display " Ite LiSP est.")
  (newline)
  (exit 0) )

;;; Warp into the new toplevel.
(start)

;;; end of book.mit
